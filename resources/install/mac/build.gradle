import org.apache.tools.ant.filters.ReplaceTokens

task copyInfoPlist(group: 'installer-mac') {
    doLast {
        // cannot be a copy task itself (the classpath must be evaluated on exec, not config)
        copy {
            from "${projectDir}/resources/install/mac/Info.plist"
            into "${installMacDist.destinationDir}/Jitsi.app/Contents/"
            filter(ReplaceTokens, tokens: [
                    CLASSPATH_STRINGS         : (files(
                            project(':launcher').configurations.runtimeClasspath,
                            project(':launcher').jar).collect {
                        "<string>\$JAVAROOT/lib/${it.name}</string>"
                    } << '<string>$JAVAROOT/config</string>'
                    ).join('\n').toString(),
                    CFBundleShortVersionString: versionShort,
                    CFBundleVersion           : versionFull,
                    JRE_VERSION               : "${project.property('application.jre.version')}-jre".toString(),
            ])
        }
    }
}

task copyMacLauncher(type: Copy, group: 'installer-mac') {
    from 'native'
    into "${installMacDist.destinationDir}/Jitsi.app/Contents/MacOS/"
    include '**/cmake-build/launcher/**/MacOS/Jitsi'
    includeEmptyDirs = false
    eachFile {
        path = name
    }
}

copyInfoPlist.mustRunAfter(installMacDist)
copyMacLauncher.mustRunAfter(installMacDist)

task createDmg(type: Exec, group: 'installer-mac', dependsOn: [
        installMacDist,
        copyMacLauncher,
        copyInfoPlist,
]) {
    // TODO: sign
    workingDir installMacDist.destinationDir.parent
    commandLine "${projectDir}/resources/install/mac/build-dmg.sh"
    args installMacDist.destinationDir.name
}
